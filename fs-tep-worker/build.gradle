buildscript {
    dependencies {
        classpath pl.osDetector
        classpath pl.osPackage
        classpath pl.springBoot
    }
}

group 'com.cgi.eoss.fs-tep'
version '1.3.8-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'jacoco'

apply plugin: 'org.springframework.boot'
apply plugin: 'nebula.ospackage'
apply plugin: 'com.google.osdetector'

dependencies {
    compileOnly 'org.projectlombok:lombok'

    compile project(path: ':fs-tep-clouds', configuration: 'shadow')
    compile project(':fs-tep-io')
    compile project(':fs-tep-logging')
    compile project(':fs-tep-queues')
    compile project(':fs-tep-rpc')

    // Pre-built shaded docker-java jar
    compile project(path: ':third-party/java/docker-java', configuration: 'shadow')

    compile 'commons-io:commons-io'
    compile 'commons-net:commons-net'
    compile 'com.lmax:disruptor'
    compile 'com.squareup.okhttp3:okhttp'
    compile 'com.squareup.okhttp3:logging-interceptor'
    compile 'io.netty:netty-tcnative-boringssl-static'
    compile 'javax.servlet:javax.servlet-api'
    compile 'org.apache.commons:commons-text'
    compile 'org.apache.logging.log4j:log4j-jul'
    compile 'org.aspectj:aspectjweaver'
    compile 'org.graylog2.log4j2:log4j2-gelf'
    compile 'org.jooq:jool'
    compile 'org.lognet:grpc-spring-boot-starter'
    compile 'org.springframework:spring-context'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.1'
    compile 'org.springframework.boot:spring-boot-autoconfigure'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-jdbc'
    compile 'org.springframework.boot:spring-boot-starter-log4j2'
    compile 'org.springframework.boot:spring-boot-starter-undertow'
    compile 'org.springframework.cloud:spring-cloud-starter-eureka'
	compile 'org.springframework.data:spring-data-jpa'
	compile 'org.hibernate:hibernate-core'
    compile 'org.hibernate:hibernate-entitymanager'
    
	testCompile project(':fs-tep-catalogue')
    testCompile project(':fs-tep-persistence')

    testCompile 'com.google.jimfs:jimfs'
    testCompile 'com.squareup.okhttp3:mockwebserver'
    testCompile 'junit:junit'
    testCompile 'org.hamcrest:hamcrest-junit'
    testCompile 'org.mockftpserver:MockFtpServer'
    testCompile 'org.mockito:mockito-core'
    testCompile 'org.springframework:spring-test'
    testCompile 'org.springframework.boot:spring-boot-test'
    
    runtime 'org.hsqldb:hsqldb'
    testRuntime 'org.hsqldb:hsqldb'
}

springBoot {
    classifier = 'bin'
    executable = true
}

// bootRepackage was depending on "archives" and creating a circular dependency
bootRepackage.dependsOn = [jar]
buildRpm.dependsOn bootRepackage
buildDeb.dependsOn bootRepackage
configurations { pkg }
artifacts { pkg buildRpm }

ospackage {
    packageName 'fs-tep-worker'
    version getRpmVersion(project.version).version
    release getRpmVersion(project.version).release
    arch NOARCH
    os LINUX

    user 'fstep'
    permissionGroup 'fstep'

    preInstall file('src/ospackage/preinst.sh')
    postInstall file('src/ospackage/postinst.sh')

    into('/var/fs-tep/worker') {
        from("${buildDir}/libs") {
            include '*-bin.jar'
            rename('fs-tep-worker-.*\\-bin.jar', 'fs-tep-worker.jar')
            fileMode 0500
        }

        from("src/ospackage/application.properties") {
            fileType CONFIG | NOREPLACE
        }
    }

    into('/usr/lib/systemd/system') {
        from 'src/ospackage/fs-tep-worker.service'
        addParentDirs false
    }
}
