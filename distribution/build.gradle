import java.nio.file.Files
import java.nio.file.Paths

group 'com.cgi.eoss.fs-tep'
version '1.2.13'

apply plugin: 'base'
apply plugin: 'org.hidetake.ssh'

configurations {
    packages
    puppetModules
}

dependencies {
    packages project(path: ':fs-tep-portal', configuration: 'pkg')
    packages project(path: ':fs-tep-ui', configuration: 'pkg')
    packages project(path: ':fs-tep-server', configuration: 'pkg')
    packages project(path: ':fs-tep-serviceregistry', configuration: 'pkg')
    packages project(path: ':fs-tep-worker', configuration: 'pkg')
    packages project(path: ':fs-tep-zoomanager', configuration: 'pkg')
    packages project(path: ':third-party/pkg', configuration: 'pkg')
    packages project(path: ':third-party/resto', configuration: 'pkg')

    puppetModules project(path: ':third-party/puppet', configuration: 'allModules')
}

task distPackages(type: Sync) {
    dependsOn configurations.packages

    into "${buildDir}/repo"

    into('6/local/noarch/RPMS') {
        from configurations.packages
        include '**/*.noarch.rpm'
    }
    into('6/local/x86_64/RPMS') {
        from configurations.packages
        include '**/*.x86_64.rpm'
    }
}

task createrepo(type: Exec) {
    dependsOn distPackages
    // TODO More rigorous checking for createrepo binary
    onlyIf { Files.isExecutable(Paths.get('/usr/bin/createrepo')) }

    def repoDir = Paths.get("${buildDir}/yumrepo")

    executable '/usr/bin/createrepo'
    args "--outputdir=${repoDir}", "${buildDir}/repo"

    inputs.files distPackages.outputs.files
    outputs.files fileTree(repoDir.toString())

    doFirst {
        Files.createDirectories(repoDir)
    }
}
ssh.settings {
  def knownHostsFile=project.hasProperty('upload.knownHosts') ? project.getProperty('upload.knownHosts'): 'knownHosts'
  knownHosts = addHostKey(file("${knownHostsFile}"))
}

remotes {
  ext.uploadHost=project.hasProperty('upload.uploadHost') ? project.getProperty('upload.uploadHost'): 'host'
  ext.uploadUser=project.hasProperty('upload.uploadUser') ? project.getProperty('upload.uploadUser'): 'user'
  def uploadIdentity=project.hasProperty('upload.uploadIdentity') ? project.getProperty('upload.uploadIdentity'): 'identity'
  upload {
    host = uploadHost
    user = uploadUser
    identity = file("${uploadIdentity}")
  }
}


task distPuppet(type: Sync) {
    dependsOn configurations.puppetModules

    into "${buildDir}/puppet"
    from 'puppet'

    into('local-modules') {
        from {
            configurations.puppetModules.collect { tarTree(it) }
        }
    }
}

task buildDist(type: Sync) {
    into "${rootDir}/.dist"

    preserve {
        include 'puppet/modules/**'
    }

    into('repo') {
        from createrepo
        from distPackages
    }
    into('puppet') {
        from distPuppet
    }
}

task deployRpms {
   ext.uploadDir=project.hasProperty('upload.uploadDir') ? project.getProperty('upload.uploadDir'): '/dev/null'
   doLast{
       ssh.run {
        session(remotes.upload) {
          remove uploadDir + '/6'
          remove uploadDir + '/repodata'
          put from: "${buildDir}/yumrepo/repodata", into: uploadDir
          put from: "${buildDir}/repo/6", into: uploadDir
        }
      }
    }
}


sonarqube {
    skipProject = true
}
